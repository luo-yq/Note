
study
中文数字转换为阿拉伯数字

@echo off
:: Code by JM 2007-1-2～1-3 bbs.cn-dos.net CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26288
:: 中文形式数字转换为阿拉伯数字
:: 思路：
:: 　　先把所有的中文数字转化为前后带空格的阿拉伯数字(零要特殊处理)，
:: 然后，把阿拉伯数字从高位到低位逐一提取出来；提取的同时，对"零"做
:: 补位及替换处理，补位的具体规则为：比较"零"前后的进位，补足相应的0，
:: 比如：某个"零"的前后进位分别为"万"和"拾"，则这个"零"则替换为两个
:: 连续的0。最后，把所有的空格去掉。
setlocal enabledelayedexpansion
set str=捌仟零叁拾万零柒佰零贰点零伍肆
echo.
echo           要处理的中文数值：%str%
echo --------------------------------------------------
:: 把中文数字转化为阿拉伯数字
for /f "tokens=1* delims=点" %%i in ("%str%") do set var1=%%i&set var2=%%j
call :replace_1 %var1%
set int=%var%
if not "%var2%"=="" (
    call :replace_1 %var2%
    set dec=!var:零=!
)
echo.
echo 中文数字阿拉伯化的结果1：%int%.%dec%
:: 提取阿拉伯数字
call :pickup %int%
:: 检查原始数值整数部分的最后一位是不是数字，从而决定是否继续转换
if not "%int:~-1%"==" " (
    set tmp=!last!
    call :replace_2
)
echo.
echo 中文数字阿拉伯化的结果2：%str_%%tmp%.%dec%
if not "%dec%"=="" (
    set result=%str_: =%%tmp%.%dec: =%
) else set result=%str_: =%%tmp%
echo.
echo --------------------------------------------------
echo            最终结果是：%result%
endlocal
pause>nul
goto :eof
:pickup
:: 提取阿拉伯数字，并对"零"做处理
set last=%2
if not %1 equ 0 (
    set str_=!str_! %1
) else (
    set str_=!str_! %zero%
    set tmp=
    set length=0
)
:: 对"×佰零×"之类的数要单独处理
if "%4"=="零" (
    if not "%2"=="佰" (
        call :replace %2 %6
    ) else set zero=0
)
shift
shift
if not "%1"=="" goto pickup
goto :eof
:replace
:: 对"零"做补位处理
if "%2"=="" (
    set tmp=
) else (
    set tmp=%2
    set tmp=!tmp:~0,1!
)
set tmp=%1 %tmp%
call :replace_2
for /f "tokens=1,2" %%i in ("%tmp%") do set former=%%i&set later=%%j
:: 比较字符串 %former% 和 %later% 的长短，不能用 if %former% lss %later% 语句
set flag=!former:%later%=!
if "%flag%"=="%former%" (
    set zero=%former:~1%
    set flag=
    goto :eof
)
:get_length
set /a length+=1
if not "%later%"=="" set later=%later:~0,-1%&goto get_length
set zero=!former:~%length%!
goto :eof
:replace_1
set var=%1
set num=1
for %%i in (壹 贰 叁 肆 伍 陆 柒 捌 玖) do (
    call set var=%%var:%%i= !num! %%
    set /a num+=1
)
set var=%var:零= 0 零 %
goto :eof
:replace_2
set tmp=%tmp:拾=0%
set tmp=%tmp:佰=00%
set tmp=%tmp:仟=000%
set tmp=%tmp:万=0000%
set tmp=%tmp:亿=00000000%
goto :eof
@echo off
:: code by youxi01 2007-1-3 CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26288
:: 说明：只能处理1亿以内的数据
setlocal enabledelayedexpansion
set str=捌仟贰佰叁拾万零柒佰零贰点零伍肆
set /a 仟=1000,佰=100,拾=10,个=1
for %%i in (壹 贰 叁 肆 伍 陆 柒 捌 玖) do (
   set/a a+=1
   call set str=%%str:%%i=!a!%%)
for /f "tokens=1* delims=点" %%i in ("%str%") do set rnd=%%i&set dec=%%j
for /f "tokens=1* delims=万" %%i in ("%rnd:零=%") do (
    if "%%j"=="" (
        set/a num1_=0
        call :test %%i 2
        ) else (call :test %%i 1 &call :test %%j 2))
for /f "tokens=2 delims==" %%i in ('set num1_') do set /a Res1+=%%i
for /f "tokens=2 delims==" %%i in ('set num2_') do set /a Res2+=%%i
set /a Res=%Res1%*10000+%Res2%
if "%dec%"=="" (echo !Res!) else echo !Res!.!dec:零=0!
pause>nul
:test
   set tmp=%1
   for /l %%i in (0 2 8) do (
       set tmp_=!tmp:~%%i,2!
       if not "!tmp_!"=="" (
           set tmp1=!tmp_:~0,1!
           set tmp2=!tmp_:~1,1!
           if "!tmp2!"=="" set tmp2=个
           call set /a num%2_%%i=!tmp1!*%%!tmp2!%%) else goto :eof)

@echo off
:: Code by tigerpower 2007-1-3  bbs.cn-dos.net CMD@XP
set USAGE=USAGE: %0 hanzi
setlocal enabledelayedexpansion
set daxie=零壹贰叁肆伍陆柒捌玖
for /l %%a in (0,1,9) do (set Arabic_poi!daxie:~%%a,1!=%%a)
for /l %%a in (0,1,9) do (set Arabic_int!daxie:~%%a,1!=+%%a)
set Arabic_poi点=.
set Arabic_int拾=*10
set Arabic_int佰=*100
set Arabic_int仟=*1000
set Arabic_int万=)*10000
set Arabic_int亿=)*100000000+(0
if "%1"=="" echo %USAGE%&&goto :eof
set input=%1。
set input=%input:零点=点%
set point=0
:loop
if not "!input!"=="" (
set chr=!input:~,1!
if "!chr!"=="点" set point=1
if "!chr!"=="万" set wan=1
if "!chr!"=="亿" set yi=1
if %point%==0 (set chr_int=!Arabic_int%chr%!&&set int=!int!!chr_int!) else (set chr_poi=!Arabic_poi%chr%!&&set poi=!poi!!chr_poi!)
set input=!input:~1!
goto loop)
if not defined int set int=0
if defined wan set int=(!int!
if defined yi (if not defined wan set int=(!int!^))
if defined yi (
for /l %%i in (0,1,22) do (
set ch=!int:~%%i,1!
if "!ch!"==")" set /a index=%%i+1&&goto next)
) else set /a int=!int!&&goto end
:next
set int_yi=!int:~0,%index%!
set /a index=%index%+11
set int_re=!int:~%index%!
set /a int_yi=%int_yi%
set /a int_re=%int_re%
for /l %%j in (0,1,8) do (
set ch=!int_re:~%%j,1!
if "!ch!"=="" set /a zero=8-%%j&&goto next2)
:next2
if defined yi (
for /l %%k in (1,1,%zero%) do set int_re=0!int_re!
set int=!int_yi!!int_re!)
:end
echo %int%%poi%
 Copyright ©2011 lyt. All Rights Reserved.








study
人民币大小写互转程序

:: 人民币大小写互转程序 R3
:: qzwqzw http://bbs.cn-dos.net
:: 2007-01-03 11:15
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26288
@echo off
setlocal EnableDelayedExpansion
for /l %%i in (1,1,100) do (
    set /a numin=!random!*50001-50000000
    set numin=!numin:~0,-2!.!numin:~-2!
    set in=!numin!
    call :Num2RMB
    set in=!out!
    call :RMB2Num
    echo.%%i [!numin!:!in!:!out!]
    if !numin! neq !out! set /p=--- Invalid ---
    set out=
)
if "%~0"=="%~f0" pause
endlocal
goto :eof
:Num2RMB
setlocal
set num=%in%
set num=0%num%
for /f "tokens=1,2,* delims=." %%f in ("%num%") do (
    set num2=%%g00
    set num=%%f!num2:~0,2!
    if not "%%h"=="" goto :eof
)
:del_pre0
if "%num:~0,1%"=="0" (
    set num=%num:~1%
    goto del_pre0
)
:checknum
set num=%num:,=%
set /a num2=num+0
if not "%num%"=="%num2%" goto :eof
if %num% geq 1000000000000 goto :eof
if %num% leq 0 goto :eof
set tbl1=零壹贰叁肆伍陆柒捌玖
set tbl2=分角元拾佰仟万拾佰仟亿拾佰仟
:n2r_loop
call set rmb=%%tbl1:~%num:~-1,1%,1%%%%tbl2:~%bit%,1%%%rmb%
set /a bit+=1
set num=%num:~0,-1%
if not "%num%"=="" goto n2r_loop
set rmb=%rmb:零拾=零%
set rmb=%rmb:零佰=零%
set rmb=%rmb:零仟=零%
set rmb=%rmb:零零=零%
set rmb=%rmb:零零=零%
set rmb=%rmb:零元=元零%
set rmb=%rmb:零万=万零%
set rmb=%rmb:零亿=亿零%
set rmb=%rmb:零零=零%
set rmb=%rmb:零分=零%
set rmb=%rmb:零角=零%
set rmb=%rmb:角零=角%
set rmb=%rmb:零零=整%
endlocal & set out=%rmb%
goto :eof
::Num2RMB
:RMB2Num
setlocal
set rmb=%in%
set rmb=%rmb:零=%
set rmb=%rmb:整=%
set tbl1=零壹贰叁肆伍陆柒捌玖
set tbl2=分角元拾佰仟万拾佰仟亿拾佰仟
for /l %%i in (1,1,9) do call set rmb%%tbl1:~%%i,1%%=%%i
:r2n_loop
if "%tbl2:~0,1%"=="%rmb:~-1,1%" (
    set rmb=!rmb:~0,-1!
    call set tmp=%%rmb!rmb:~-1,1!%%
    if not "!tmp!"=="" (
        set num=!tmp!!num!
        set rmb=!rmb:~0,-1!
    ) else (
        set num=0!num!
    )
) else (
    set num=0!num!
)
set tbl2=%tbl2:~1%
if not "%rmb%"=="" if not "%tbl2%"=="" goto r2n_loop
set num=%num:~0,-2%.%num:~-2%
endlocal & set out=%num%
goto :eof
::RMB2Num
 Copyright ©2011 lyt. All Rights Reserved.






study
人民币金额小写转大写

:: 人民币金额小写转大写
:: qzwqzw@bbs.cn-dos.net
:: 2007-01-01
::出处：http://www.cn-dos.net/forum/viewthread.php?tid=26230
@echo off
setlocal EnableDelayedExpansion
set tbl1=零壹贰叁肆伍陆柒捌玖
set tbl2=分角元拾佰仟万拾佰仟亿拾佰仟
:test_
setlocal
set /p num=请输入金额（小于一万亿元）:
for /f "tokens=1,2 delims=." %%f in ("%num%") do (
    set num2=%%g00
    set num=%%f!num2:~0,2!
)
:loop
call set rmb=%%tbl1:~%num:~-1,1%,1%%%%tbl2:~%bit%,1%%%rmb%
set /a bit+=1
set num=%num:~0,-1%
if not "%num%"=="" goto loop
set rmb=%rmb:零拾=零%
set rmb=%rmb:零佰=零%
set rmb=%rmb:零仟=零%
set rmb=%rmb:零零=零%
set rmb=%rmb:零零=零%
set rmb=%rmb:零元=元零%
set rmb=%rmb:零万=万零%
set rmb=%rmb:零亿=亿零%
set rmb=%rmb:零零=零%
set rmb=%rmb:零分=零%
set rmb=%rmb:零角=零%
set rmb=%rmb:角零=角%
set rmb=%rmb:零零=整%
echo 人民币%rmb%
endlocal
goto test_
:: 数字金额转大写
::code by youxi01@cn-dos.net
::date 2006-1-1(Happy new year!best wishes to everyone!)
::出处：http://www.cn-dos.net/forum/viewthread.php?tid=26230
@echo off
setlocal enabledelayedexpansion
set /a a=0,b=0,c=0
::================================
::设置单位名称；
SET NAME0=仟
SET NAME1=佰
SET NAME2=拾
SET NAME3=
::================================
::=============================================
::设置数字对应的大写中文汉字
for %%i in (零 壹 贰 叁 肆 伍 陆 柒 捌 玖) do (
     set BIG!a!=%%i
     set /a a+=1)
::=============================================
::=======================================================================
::在以下“函数”的处理过程中，要用到"#"对数字进行对齐(都成四位)，以便截取；
::这里设置凡是出现"#"的地方都设置为空。
set BIG#=
::=======================================================================
set EN=
::====================================================
::这里对输入的数字进行处理，分别取出整数部分和小数部分;
set /p EN=请输入金钱数(1000亿以内)：
for /f "tokens=1,2* delims=." %%i in ("%EN%") do (
      set "round=%%i"
      set "dec=%%j00" 2>nul)
::=====================================================
set /a round=%round:,=%
set dec=%dec:~0,2%
:test
   set /a b+=1
   ::=============================================
   ::每四位数字为一组，对原来的数据进行截取；
   if %round% gtr 9999 (
      set num!b!=!round:~-4!
      set round=!round:~0,-4!
      goto :test) else set num!b!=!round!
   ::==============================================
::=====================================================
::分别对1、2、3段数据进行处理；分别赋予单位：元、万、亿
::同时，分别将返回的数据传给str1,str2,str3(利用%3来控制)。
call :test1 %num1% 元 1
call :test1 %num2% 万 2
call :test1 %num3% 亿 3
::======================================================
::====================================================================
::去掉数字大写里多余的"零"。比如，1002，处理后读出来为："壹千零贰元整"
::符合我们中国人一般的读数方法;同时将结果分别传入str1,str2,str3(%2控制)。
call :test2 %str1% 1
call :test2 %str2% 2
call :test2 %str3% 3
::=====================================================================
::==============================================================
::防止类似2,0000,1002.00形式出现错误(错误读为：2亿万1千零2元整)
if "!str2!"=="零万" set str2=零
::===============================================================
set str=%str3%%str2%%str1%
set str=%str:零元=元%
set str=%str:零万=万%
set str=%str:零亿=亿%
::防止整数部分为0；
if "%str%"=="元" set str=零元
::对小数部分数字进行处理；
if "%dec%"=="00" (set dec=整) else (
    set /a dec1=!dec:~0,1!
    set /a dec2=!dec:~1,1!
    if !dec1! EQU 0 (set dec1=零) else call set dec1=%%BIG!dec1!%%角
    if !dec2! EQU 0 (set dec2=) else call set dec2=%%BIG!dec2!%%分
    set dec=!dec1!!dec2!
)
echo.
echo ========================
echo 你输入的金钱数目大写为：
echo.
echo %str:零零=零%%dec%
echo ========================
pause>nul
:test1
    if not "%1"=="" (
        set temp=####%1
        set temp=!temp:~-4!
        for /l %%i in (0 1 3) do (
               set tmp%%i=!temp:~%%i,1!
               if defined tmp%%i (
                  if !tmp%%i! GTR 0 (call set str%3=!str%3!%%BIG!tmp%%i!%%!NAME%%i!) else (
                         call set str%3=!str%3!%%BIG!tmp%%i!%%)))
       set str%3=!str%3!%2
       ) else set str%3=
     goto :eof
:test2
     set tmp=%1
     set tmp=%tmp:零零=零%
     set str%2=%tmp:零零=零%
     goto :eof
 Copyright ©2011 lyt. All Rights Reserved.





study
把秒转换为天小时分秒的格式

@echo off
:: 把秒数转换为×天×小时×分×秒的格式
:: Code by zh159 2007-1-16 CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26758
:begin
cls
echo.
set input=
set /p input=  请输入秒数(退出请直接按回车)：
if not defined input exit
set /a SS=%input%%%60
set /a MM=%input%/60%%60
set /a HH=%input%/3600%%60
set /a DD=%input%/86400
echo.
echo   %DD% 天 %HH% 小时 %MM% 分 %SS% 秒
echo.
pause
goto begin
@echo off
:: Code by JM 2007-1-16 CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26758
:begin_
cls
set /a D=0,H=0,M=0
set num=
set /p num=     请输入秒数(退出请直接按回车)：
if not defined num exit
call :transform D %num% 86400
call :transform H %num% 3600
call :transform M %num% 60
echo       %D% 天 %H% 小时 %M% 分 %num% 秒
pause
goto begin_
:transform
set /a %1=%2/%3
if %1 gtr 0 set /a num=%2-%3*%1
goto :eof
@echo off
:: Code by tao0610 2007-1-16 CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26758
:start
setlocal
cls&echo.
set/p input=请输入秒数:
for /f "tokens=1,2 delims=." %%a in ("%input%") do set input=%%a&set input1=%%b
for /l %%a in (1,1,2) do set/a T%%a=input%%60,input/=60
set/a T3=input%%24,T4=input/24
set input1=%input1%0000
for /f "tokens=* delims=0" %%a in ("%input1:~0,4%") do set T0=%%a
echo/
if defined T0 (echo %T4% 天 %T3% 小时 %T2% 分 %T1% 秒 %T0% 毫秒
) else (echo %T4% 天 %T3% 小时 %T2% 分 %T1% 秒)
echo\
pause
endlocal
goto start
 Copyright ©2011 lyt. All Rights Reserved.







study
阿拉伯与罗马数字互转程序

:: 阿拉伯与罗马数字互转程序
:: qzwqzw http://bbs.cn-dos.net
:: 2007-01-03 Revision 2
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26230
@echo off
setlocal EnableDelayedExpansion
for /l %%i in (1,1,100) do (
    set /a numin=!random!*4200/32768-100
    set in=!numin!
    set out=
    call :Arab2Roman
    set in=!out!
    call :Roman2Arab
    echo.%%i [!numin!:!in!:!out!]
    if !numin! neq !out! set /p=--- Invalid ---
)
pause
endlocal
goto :eof
:Arab2Roman
setlocal EnableDelayedExpansion
set num=%in%
set /a tmp=num+0
if not "%num%"=="%tmp%" goto :eof
if %num% geq 4000 goto :eof
if %num% leq 0 goto :eof
set i=0
for %%v in (1000 900 500 400 100 90 50 40 10 9 5 4 1) do (
    set /a i+=1
    set arab!i!=%%v
)
set i=0
for %%v in (M CM D CD C XC L XL X IX V IV I) do (
    set /a i+=1
    set roman!i!=%%v
)
set i=1
:a2r_loop
call set tmp=%%arab%i%%%
if 1%num% lss 1%tmp% (
    set /a i+=1
) else (
    call set rom=%rom%%%roman%i%%%
    set /a num-=tmp
)
if 1%num% gtr 10 goto a2r_loop
endlocal & set out=%rom%
goto :eof
::Arab2Roman
:Roman2Arab
setlocal EnableDelayedExpansion
echo.%in%|findstr /r "[^IVXLCDMivxlcdm]">nul && goto :eof
set i=0
for %%v in (1000 900 500 400 100 90 50 40 10 9 5 4 1) do (
    set /a i+=1
    set arab!i!=%%v
)
set i=0
for %%v in (M 1 D 2 C 3 L 4 X 5 V 6 I) do (
    set /a i+=1
    set roman!i!=%%v
)
set in=%in:m=M%
set in=%in:d=D%
set in=%in:c=C%
set in=%in:l=L%
set in=%in:x=X%
set in=%in:v=V%
set in=%in:i=I%
set in=%in:CM=1%
set in=%in:CD=2%
set in=%in:XC=3%
set in=%in:XL=4%
set in=%in:IX=5%
set in=%in:IV=6%
set i=1
set num=0
:r2a_loop
set tmp=%in:~0,1%
call set rom1=%%roman%i%%%
if "%tmp%"=="%rom1%" (
    call set /a num+=arab%i%
    set in=%in:~1%
) else (
    set /a i+=1
)
if %i% gtr 13 goto :eof
if not "%in%"=="" goto r2a_loop
endlocal & set out=%num%
goto :eof
::Roman2Arab
 Copyright ©2011 lyt. All Rights Reserved.






study
阿拉伯数字转为罗马数字

:: Arab2Roman 阿拉伯数字转为罗马数字
:: qzwqzw @ http://bbs.cn-dos.net
:: 2007-01-02 Revision 3
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26230
@echo off
setlocal EnableDelayedExpansion
:test
if "%~0"=="%~f0" cls
setlocal
set /p num=请输入阿拉伯数字num（"0<num<4000"，直接回车退出）:
if "%num%"=="" goto :eof
:checknum
set /a tmp=num+0
if "%num%"=="%tmp%" if %num% lss 4000 if %num% gtr 0 goto Arab2Roman
:error
echo.
echo 无效的数字
echo.
if "%~0"=="%~f0" pause
endlocal
goto test
:Arab2Roman
set i=0
for %%v in (1000 900 500 400 100 90 50 40 10 9 5 4 1) do (
    set /a i+=1
    set arab!i!=%%v
)
set i=0
for %%v in (M CM D CD C XC L CL X IX V IV I) do (
    set /a i+=1
    set roman!i!=%%v
)
set i=1
:loop
call set tmp=%%arab%i%%%
if 1%num% lss 1%tmp% (
    set /a i+=1
) else (
    call set rom=%rom%%%roman%i%%%
    set /a num-=tmp
)
if 1%num% gtr 10 goto loop
::Arab2Roman
echo.
echo %rom%
echo.
if "%~0"=="%~f0" pause
endlocal
goto test
 Copyright ©2011 lyt. All Rights Reserved.





study
十进制转二进制

@echo off
:: 十进制转二进制
:: Code by 0401 2007-1-6 CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26399
setlocal enabledelayedexpansion
set/p scanf=能正确处理的最大数为[2147483647]，请输入需要转换的十进制数：
if not defined scanf exit/b
set dec=%scanf%
call :d2b
echo %dec% 的二进制为：%bin%
pause>nul
exit/b
:d2b
set a=1
for /l %%i in (1,1,31) do (
        set/a t=%scanf%"&"!a!
        set/a a*=2
        if !t! equ 0 (set bin=0!bin!) else (set bin=1!bin!)
)
if %bin% equ 0 set bin=0 & goto :eof
:cut0
if %bin:~0,1% equ 0 set bin=%bin:~1%&goto :cut0
 Copyright ©2011 lyt. All Rights Reserved.




study
十进制转十六进制

@echo off
:: 10进制转换为16进制
:: code by 3742668 2006-6-28 CMD@XP
set /p 源数=输入十进制数字:
set /a 源数=%源数% || goto :eof
:dosomething
    set /a 余数 = %源数% %% 16
    set /a 源数 /= 16
    call :转换 %余数% 
    set 余数=%ret%
    set 计算结果=%余数%%计算结果%
    if %源数% lss 16 goto end
goto dosomething
:转换
    set ret=
    if "%1" == "10" set ret=A
    if "%1" == "11" set ret=B
    if "%1" == "12" set ret=C
    if "%1" == "13" set ret=D
    if "%1" == "14" set ret=E
    if "%1" == "15" set ret=F
    if %1 lss 10 set ret=%1
goto :eof
:end
call :转换 %源数%
set 源数=%ret%
if "%源数%" == "0" set 源数=
echo  0x%源数%%计算结果% 
set ret=
set 源数=
set 余数=
set 计算结果=
pause>nul
goto :eof
@echo off
:: Code by 0401 2007-1-6 CMD@XP
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26399
setlocal enabledelayedexpansion
set hexstr=0 1 2 3 4 5 6 7 8 9 A B C D E F
set d=0
for %%i in (%hexstr%) do (set d!d!=%%i&set/a d+=1)
set/p scanf=能正确处理的最大数为[2147483647]，请输入需要转换的十进制数：
if not defined scanf exit/b
set dec=%scanf%
call :d2h
if not defined hex set hex=0
echo %dec% 的十六进制为：0x%hex%
pause>nul
exit/b
:d2h
if %scanf% equ 0 exit/b
set/a tscanf=%scanf%"&"15
set/a scanf">>="4
set hex=!d%tscanf%!!hex!
goto :d2h
 

 
生成0-99之间的随机数列

:: 生成0-99之间的随机数列 R1
:: qzwqzw@bbs.cn-dos.net
:: 2007-01-04 23:03
:: 出处：http://www.cn-dos.net/forum/viewthread.php?tid=26330
@echo off
setlocal EnableDelayedExpansion
:: 初始化顺序数列
for /l %%i in (0,1,99) do (
    set rnum%%i=%%i
)
:: 对数列进行随机交换
for /l %%i in (0,1,99) do (
    set /a rnd=!random! %% 100
    call set tmp=%%rnum!rnd!%%
    set rnum!rnd!=!rnum%%i!
    set rnum%%i=!tmp!
)
set rnum
pause
  

 